apply plugin: 'com.android.application'

android {

    signingConfigs {

        release {

            keyAlias 'snake'
            keyPassword '9895204814'
            storeFile file('../keystore.jks')
            storePassword '9895204814'
        }
    }

    compileSdkVersion 30
    buildToolsVersion '30.0.3'

    defaultConfig {

        applicationId "ndk.personal.account_ledger"

        minSdkVersion 19
        targetSdkVersion 30

        versionCode 1
        versionName "1.0"

        multiDexEnabled true
    }

    buildTypes {

        debug {

            applicationIdSuffix ".debug"
        }

        release {

            signingConfig signingConfigs.release
            // shrinkResources true
            // minifyEnabled true
            // proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        //The `initWith` property allows you to copy configurations from other build types,then configure just the settings you want to change.
        staging {

            initWith release
            debuggable true
        }
    }

    // Specifies flavor dimension.
    flavorDimensions "relevence"
    productFlavors {

        // When building a variant that uses this flavor, the following configurations override those in the defaultConfig block.

        development {

            dimension "relevence"

            // To avoid using legacy multidex when building from the command line, set minSdkVersion to 21 or higher. When using Android Studio 2.3 or higher, the build automatically avoids legacy multidex when deploying to a device running API level 21 or higherâ€”regardless of what you set as your minSdkVersion.
//            minSdkVersion 21

            // The following configuration limits the usage of mdpi screen-density resources.
            resConfigs "mdpi"
        }

        production {

            dimension "relevence"
            // If you've configured the defaultConfig block for the release version of your app, you can leave this block empty and Gradle uses configurations in the defaultConfig block instead. You still need to create this flavor. Otherwise, all variants use the "development" flavor configurations.
        }
    }

    variantFilter { variant ->

        def names = variant.flavors*.name

        // To check for a certain build type, use variant.buildType.name == "<buildType>"
        if (names.contains("development") && names.contains("release")) {

            // Gradle ignores any variants that satisfy the conditions above.
            setIgnore(true)
        }

        if (names.contains("production") && names.contains("debug")) {

            // Gradle ignores any variants that satisfy the conditions above.
            setIgnore(true)
        }

        if (names.contains("development") && names.contains("staging")) {

            // Gradle ignores any variants that satisfy the conditions above.
            setIgnore(true)
        }
    }

    compileOptions {

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

//    viewBinding.enabled = true

//    dataBinding {
//
//        enabled = true
//    }

    lintOptions {
        abortOnError false
    }
}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.4.0'

    implementation 'org.apache.commons:commons-lang3:3.12.0'

    implementation 'com.github.Kunzisoft:Android-SwitchDateTimePicker:2.0'
    implementation 'com.github.bmelnychuk:atv:1.2.9'
    implementation 'com.github.johnkil.print:print:1.3.1'
    implementation 'org.javatuples:javatuples:1.2'

    implementation project(path: ':DebugUtils:common')
    implementation project(path: ':JavaUtils')
    implementation project(path: ':Utils1:common1')
    implementation project(path: ':Utils3:common3')
    implementation project(path: ':Utils7:common7')
    implementation project(path: ':Utils9:common9')
    implementation project(path: ':Utils11:common11')
    implementation project(path: ':Utils14:common14')
    implementation project(path: ':Resources:common')
    implementation project(path: ':Utils16:common16')
    implementation project(path: ':Utils17:common17')
    implementation project(path: ':Utils19:common19')
}
